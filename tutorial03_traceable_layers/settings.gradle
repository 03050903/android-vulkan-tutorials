include ':app', ':nativeactivity'
project(':nativeactivity').projectDir = new File(settingsDir, '../common/nativeactivity')

// configure pre-configured layer project inside NDK-R12 (and later)
Properties properties = new Properties()
properties.load(new File(rootDir.absolutePath + "/local.properties").newDataInputStream())
def ndkDir = properties.getProperty('ndk.dir')
def layerProjRoot =
        file("${ndkDir}/sources/third_party/vulkan/src/build-android/generated/gradle-build")
String[] layers = ['threading',
                   'parameter_validation',
                   'object_tracker',
                   'core_validation',
                   'device_limits',
                   'image',
                   'swapchain',
                   'unique_objects']
for (layer in layers) {
    include ":"+ layer
    project(":" + layer.toString()).projectDir =
            new File("${layerProjRoot}/${layer}")
}
